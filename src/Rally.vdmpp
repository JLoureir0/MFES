class Rally

	values
		public INIT_AT_ZERO = 0;
		public TIME_PENALTY = 50;

	types
		public String = seq1 of char;
		public RealNumber = real;
		public RallyCategory = Type`RallyType;
		public RallyDays = seq1 of Day;
		public RallyParticipants = set1 of Team;
		public RallyWinner = Team;
		public RallyTeamSections = map Team to seq of Section;
		public MapTeamsStats = map Team to Stat;

	instance variables
	
		public category: RallyCategory;
		public local: String;
		public days: RallyDays;
		public participants: RallyParticipants;
		public totalDistance : RealNumber := INIT_AT_ZERO;
		public winner: RallyWinner;
		public teamSections: RallyTeamSections := {|->};
		public teamsStats: MapTeamsStats := {|->};
		
	operations

		public Rally: RallyCategory * String * RallyDays * RallyParticipants ==> Rally 
			Rally(c, l, d, p) == (
				category := c;
				local := l;
				days := d;
				participants := p;
				sumDaysDistances();
				return self;
			)
		pre len d > 0 and card p > 1
		post self.days = d and self.participants = p;
		
		public assignWinner: Team ==> ()
			assignWinner(w) == (
				winner := w;
			)
		pre w in set participants  
		post winner = w;
		
		public mapTeamSections: () ==> () 
			mapTeamSections() == (
				for d in days do
					for all t in set d.teams do
						(
						if(t in set dom teamSections)
							then teamSections(t) := teamSections(t) ^ d.sections
						else teamSections := teamSections munion {t |-> d.sections};
						)
			);
		
		public sumDaysDistances: () ==> ()
			sumDaysDistances() == (
				for d in days do
					(
						d.sumSectionsDistances();
						totalDistance := totalDistance + d.totalDistance;
					);
			);
		
		public addTeamRecord: Team * RealNumber ==> () 
			addTeamRecord(team, time) == (
				teamsStats := teamsStats munion {team |-> new Stat(time, totalDistance)};
			)
		pre time > 0 and team in set participants
		post team in set dom teamsStats;
		
		--Updates rally stats through all days
		public getTeamsRecords: () ==> ()
			getTeamsRecords() == (
				for d in days do
					for all t in set dom d.teamsStats do
						(
						if(t in set dom teamsStats)
							then teamsStats(t).updateStat(d.teamsStats(t).time, d.teamsStats(t).distance)
						else teamsStats := teamsStats munion {t |-> d.teamsStats(t)};
						)
			);
	
end Rally