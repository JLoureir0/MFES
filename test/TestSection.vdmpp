class TestSection is subclass of TestSuite

	instance variables
		s: Section := new Section("Venezia", "Bologna", 248.41);
		p: PointOfInterest := new PointOfInterest("Colosseo", 20, <Locality>);
		sc: Sector := new Sector("Piazza D'el Mar", 20);
	
	operations
	  public testConstructor() == (
	    IO`println("\t\t test constructor");
	    assertEqual("Venezia", s.startLocation);
	    assertEqual("Bologna", s.endLocation);
	    assertEqual(248.41, s.distance);
	   );
	   
		public testAddPointOfInterest() == (
	    IO`println("\t\t test add point of interest");
	    s.addPointOfInterest(p);
	    assertTrue(p in set s.pointsOfInterest);
	   );
	   
		public testRemovePointOfInterest() == (
	  	IO`println("\t\t test remove point of interest");
	  	s.removePointOfInterest(p);
	    assertTrue(p not in set s.pointsOfInterest);
	  );
	  
		public testAddSector() == (
	    IO`println("\t\t test add sector");
	    s.addSector(sc);
	    assertTrue(sc in set s.sectors);
	   );
	   
		public testRemoveSector() == (
	  	IO`println("\t\t test remove sector");
	  	s.removeSector(sc);
	    assertTrue(sc not in set s.sectors);
	  );
	  
	  public testTeamRecord() == (
	  	
	  	IO`println("\t\t test team record");
	  	
	  	(
	  		dcl p1: Pilot := new Pilot("SÃ©bastien", "Loeb", 43, <Male>, <Pilot>);
				dcl cp1: Pilot := new Pilot("Daniel", "Elena", 45, <Male>, <CoPilot>);
				dcl c1: Car := new Car("Ford", 1271.71, 2.5, 275.5);
				dcl t1: Team := new Team("M-Sport World Rally Team", p1, cp1, c1);
				
				s.addTeamRecord(t1, 300);
				
				--IO`printf("\t\t Team1 Stat: %s\n", [s.teamsStats(t1)]);
				
				assertTrue(t1 in set dom s.teamsStats);
				assertEqual(248.41/300, s.teamsStats(t1).averageSpeed);
	  	)
	  	
	  );
	
	  public testAll() == (
	    IO`println("\t section tests");
	    testConstructor();
	    testAddPointOfInterest();
	    testRemovePointOfInterest();
	    testAddSector();
	    testRemoveSector();
	    testTeamRecord();
	  );

end TestSection